/*
 * This file is part of BlockWeb Builder [https://github.com/TS-Code-Editor/BlockWebBuilder].
 *
 * License Agreement
 * This software is licensed under the terms and conditions outlined below. By accessing, copying, modifying, or using this software in any way, you agree to abide by these terms.
 *
 * 1. **  Copy and Modification Restrictions  **
 *    - You are not permitted to copy or modify the source code of this software without the permission of the owner, which may be granted publicly on GitHub Discussions or on Discord.
 *    - If permission is granted by the owner, you may copy the software under the terms specified in this license agreement.
 *    - You are not allowed to permit others to copy the source code that you were allowed to copy by the owner.
 *    - Modified or copied code must not be further copied.
 * 2. **  Contributor Attribution  **
 *    - You must attribute the contributors by creating a visible list within the application, showing who originally wrote the source code.
 *    - If you copy or modify this software under owner permission, you must provide links to the profiles of all contributors who contributed to this software.
 * 3. **  Modification Documentation  **
 *    - All modifications made to the software must be documented and listed.
 *    - the owner may incorporate the modifications made by you to enhance this software.
 * 4. **  Consistent Licensing  **
 *    - All copied or modified files must contain the same license text at the top of the files.
 * 5. **  Permission Reversal  **
 *    - If you are granted permission by the owner to copy this software, it can be revoked by the owner at any time. You will be notified at least one week in advance of any such reversal.
 *    - In case of Permission Reversal, if you fail to acknowledge the notification sent by us, it will not be our responsibility.
 * 6. **  License Updates  **
 *    - The license may be updated at any time. Users are required to accept and comply with any changes to the license.
 *    - In such circumstances, you will be given 7 days to ensure that your software complies with the updated license.
 *    - We will not notify you about license changes; you need to monitor the GitHub repository yourself (You can enable notifications or watch the repository to stay informed about such changes).
 * By using this software, you acknowledge and agree to the terms and conditions outlined in this license agreement. If you do not agree with these terms, you are not permitted to use, copy, modify, or distribute this software.
 *
 * Copyright Â© 2024 Dev Kumar
 */

plugins {
    id "com.android.application"
}

def KEYSTOREPASSWORD = System.getenv('KEYSTOREPASSWORD')
def secretsPropertiesFile = file('../secrets.properties')
def secretsProperties = new Properties()

if (secretsPropertiesFile.exists()) {
    secretsProperties.load(new FileInputStream(secretsPropertiesFile))
}

def analytics = System.getenv("GOOGLE_ANALYTICS")
def analyticsEnabled = analytics != null
def useEnvForFirebase = false

if (analyticsEnabled) {
    useEnvForFirebase = true
} else if (secretsPropertiesFile.exists()) {
    if (secretsProperties.getProperty('useFirebase') != null) {
        analyticsEnabled = secretsProperties.getProperty('useFirebase') == "true"
        if (analyticsEnabled) {
            useEnvForFirebase = false
            analyticsEnabled = true
        }
    }
}

def signingEnabled = false
def keyStorePassword = ""

if (KEYSTOREPASSWORD != null) {
    keyStorePassword = KEYSTOREPASSWORD
    signingEnabled = true
} else if (secretsPropertiesFile.exists()) {
    if (secretsProperties.getProperty('KEYSTOREPASSWORD') != null) {
        keyStorePassword = secretsProperties.getProperty('KEYSTOREPASSWORD')
        signingEnabled = true
    }
}

if (analyticsEnabled) {
    apply plugin: "com.google.gms.google-services"
}

def localPropertiesFile = file('../local.properties')
def localProperties = new Properties()
def useLocalProperties = false

if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
    useLocalProperties = true
}

android {
    namespace 'com.block.web.builder'
    compileSdk 34
    buildToolsVersion "34.0.0"

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getCommitSha = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "HEAD"
            standardOutput = stdout
        }
        return "\"" + stdout.toString().trim() + "\""
    }

    defaultConfig {
        applicationId "com.block.web.builder"
        minSdk 21
        targetSdk 28
        compileSdkVersion 34
        versionCode 1
        versionName "1.0 Alpha01-Snapshot-" + getCommitHash()
        buildConfigField "String", "commitSha", getCommitSha()
        buildConfigField "boolean", "isRelease", "false"
        vectorDrawables { 
            useSupportLibrary true
        }
        if (useLocalProperties) {
            if (localProperties.getProperty('enableDeveloperBlocks') != null) {
                buildConfigField "boolean", "enableDeveloperBlocks", localProperties.getProperty('enableDeveloperBlocks')
            } else {
                buildConfigField "boolean", "enableDeveloperBlocks", "false"
            }
        } else {
            buildConfigField "boolean", "enableDeveloperBlocks", "false"
        }
    }

    signingConfigs {
        release {
            if (signingEnabled) {
                storeFile file("../keystore.jks")
                storePassword keyStorePassword
                keyAlias "BlockWebBuilder"
                keyPassword keyStorePassword
            }
        }
        debug {
            if (signingEnabled) {
                storeFile file("../keystore.jks")
                storePassword keyStorePassword
                keyAlias "BlockWebBuilder"
                keyPassword keyStorePassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs_nio:2.0.2")

    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.google.android.material:material:1.11.0"
    implementation "androidx.core:core-splashscreen:1.0.1"
    if (analyticsEnabled) {
        if (useEnvForFirebase) {
            new File("$projectDir/google-services.json").text = analytics;
        }
    }

    implementation "com.vanniktech:android-image-cropper:4.5.0"

    def editorGroupId = "io.github.Rosemoe.sora-editor"
    implementation platform("$editorGroupId:bom:0.23.4")
    implementation "$editorGroupId:editor"
    implementation "$editorGroupId:language-textmate"

    implementation 'org.nanohttpd:nanohttpd:2.3.0'

    implementation platform('com.google.firebase:firebase-bom:32.3.1')
    implementation 'com.google.firebase:firebase-analytics'

    implementation project(path:':common')
    implementation project(path:':editor')
    implementation project(path:':block:core')
}
